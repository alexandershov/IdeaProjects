load("@rules_python//python:defs.bzl", "py_binary", "py_library", "py_test")

# by default files are visible only in current package
# exports_files allows files to be visible in other packages
exports_files(["file_in_subpackage.txt"])

py_binary(
    name = "check_runfiles",
    srcs = [":check_runfiles.py"],
    data = [
        "//:bazel.md",
        "//subpackage:file_in_subpackage.txt",
    ],
)

py_binary(
    name = "py_cat",
    srcs = [":py_cat.py"],
    visibility = ["//visibility:public"],
)

py_library(
    name = "adder",
    srcs = ["adder.py"],
    imports = ["."],
)

py_test(
    name = "passing_test",
    srcs = ["test_subpackage.py"],
    # flaky reruns test 3 times, if it succeeds, then tests pass
    flaky = True,
    main = "test_subpackage.py",
    # no-cache tag supposed to disable test caching
    # but it doesn't work as you expect (welcome to bazel!)
    # since action cache, where action results are stored, is ignoring this tag
    # you can work around this by passing --nouse_action_cache
    # but it's no clear why we would need no-cache tag in this case
    #    tags = ["no-cache"],
    # external tag forces test executing without any cache
    # this _really_ ignores all the caches including action cache
    tags = [
        "external",
        # block-network restricts sandbox to only using localhost for network
        # see test_block_network_tag test for a test that'll be affected by this tag
        # there's also global option --sandbox_default_allow_network=false
        # and a tag "requires-network" that allow to override sandbox_default_allow_network
        # for a specific test
        "block-network",
    ],
    deps = [
        ":adder",
    ],
)

py_test(
    name = "linux_x86_64_passing_test",
    srcs = ["test_subpackage.py"],
    main = "test_subpackage.py",
    # if we run `bazel test //...` on a mac, then it'll be skipped, since it requires
    # both x86_64 AND linux
    # default of target_compatible_with is [], which means that there are no constraints
    # you can use `select` with `target_compatible_with` for the more complex conditions:
    # e.g.: this makes target compatible with either Mac OR linux
    #    target_compatible_with = select(
    #        {
    #            "@platforms//os:macos": [],
    #            "@platforms//os:linux": [],
    #            "//conditions:default": ["@platforms//:incompatible"],
    #        },
    #    ),
    target_compatible_with = [
        "@platforms//cpu:x86_64",
        "@platforms//os:linux",
        # :incompatible is a special constraint that is incompatible with everything
        #        "@platforms//:incompatible",
    ],
    deps = [
        ":adder",
    ],
)
